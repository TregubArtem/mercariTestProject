apply plugin: "com.android.application"
apply plugin: "kotlin-android"

apply plugin: "io.fabric"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {
    compileSdkVersion targetSdk
    flavorDimensions "api"

    defaultConfig {
        applicationId "tregub.eShop"

        minSdkVersion minSdk
        targetSdkVersion targetSdk

        versionCode 5
        versionName "0"

        resConfigs "en", "ja"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        developer {
            storeFile file(keystoreProperties["storeFile"])

            storePassword keystoreProperties["storePassword"]
            keyPassword keystoreProperties["keyPassword"]
            keyAlias keystoreProperties["keyAlias"]
        }
    }
    productFlavors {
        mock {
            dimension "api"
            versionNameSuffix ":mockApi"
        }
        prod {
            dimension "api"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.developer
            versionNameSuffix ":debug"
            debuggable true

            ext.alwaysUpdateBuildId = false
        }
        release {
            signingConfig signingConfigs.developer

            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            ext.betaDistributionReleaseNotesFilePath = "distribution.notes"
            ext.betaDistributionGroupAliasesFilePath = "distribution-release.testers"
        }
        qa {
            initWith release

            versionNameSuffix new Date().format(":MMdd-HH")
            debuggable true

            ext.betaDistributionReleaseNotesFilePath = "distribution.notes"
            ext.betaDistributionGroupAliasesFilePath = "distribution-qa.testers"
        }
    }
    compileOptions {
        encoding "UTF-8"

        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    androidExtensions {
        experimental true
    }
    dataBinding {
        enabled true
    }
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$vKotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$vCoroutines"

    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.appcompat:appcompat:1.2.0-alpha01"
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha01"
    implementation "com.google.android.material:material:1.2.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-rc03"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc03"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03"

    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"

    implementation "com.squareup.retrofit2:retrofit:$vRetrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$vRetrofit"

    implementation "com.squareup.okhttp3:okhttp:$vOkhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$vOkhttp"

    kapt "androidx.databinding:compiler:3.2.0-alpha11"

    debugImplementation("androidx.fragment:fragment-testing:1.2.0-alpha01") {
        exclude group: "androidx.test", module: "core"
    }

    testImplementation "junit:junit:4.12"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$vCoroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$vCoroutines"

    androidTestImplementation "androidx.test:core:$vTestCore"
    androidTestImplementation "androidx.test:rules:$vTestCore"
    androidTestImplementation "androidx.test:runner:$vTestCore"
    androidTestImplementation "androidx.test.ext:junit:1.1.2-alpha03"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation("androidx.fragment:fragment-testing:1.2.0-alpha01") {
        exclude group: "androidx.test", module: "core"
    }
}